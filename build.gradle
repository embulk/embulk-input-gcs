plugins {
    id "java"
    id "checkstyle"
    id "jacoco"
    id "signing"
    id "maven-publish"
    id "org.embulk.embulk-plugins" version "0.7.0"
}

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = "org.embulk"
version = "0.5.0-SNAPSHOT"
description = "Reads files stored on Google Cloud Storage."

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
    options.encoding = "UTF-8"
}

java {
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    compileOnly "org.embulk:embulk-spi:0.11"

    implementation('org.embulk:embulk-util-config:0.3.1') {
        // Conflict with Embulk Core
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-annotations'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-core'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
        exclude group: 'com.fasterxml.jackson.datatype', module: 'jackson-datatype-jdk8'
        exclude group: 'javax.validation', module: 'validation-api'
    }

    // embulk-input-gcs:0.3.2 and earlier have been working with containing jackson-core:2.9.6 (no other Jackson sublibrarires)
    // while embulk-core has had jackson-core/annotations/databind 2.6.7. Safer to have the combat-tested version as of now.
    //
    // It (and other Jackson sublibraries) will be replaced with the latest Jackson after Embulk v0.10.32 and later get popular.
    implementation "com.fasterxml.jackson.core:jackson-core:2.9.6"

    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.6.7'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.6.7'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.6.7'
    implementation 'javax.validation:validation-api:1.1.0.Final'

    implementation "org.embulk:embulk-util-file:0.1.3"
    implementation('org.embulk:embulk-util-retryhelper:0.8.1') {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }

    implementation("com.google.cloud:google-cloud-storage:1.56.0") {
        exclude group: "commons-logging", module: "commons-logging"
    }

    // Instead of "commons-logging:commons-logging:1.2" that is required from "google-cloud-storage",
    // we use "jcl-over-slf4j" because Embulk is based on SLF4J.
    implementation("org.slf4j:jcl-over-slf4j:1.7.12") {
        // slf4j-api is in embulk-core / embulk-api.
        exclude group: "org.slf4j", module: "slf4j-api"
    }

    testImplementation "junit:junit:4.12"
    testImplementation "org.embulk:embulk-core:0.11.4"
    testImplementation "org.embulk:embulk-deps:0.11.4"
    testImplementation "org.embulk:embulk-junit4:0.11.4"
    testImplementation "org.mockito:mockito-core:2.18.3"
    testImplementation 'com.google.code.findbugs:annotations:3.0.0'
    testImplementation "org.embulk:embulk-formatter-csv:0.10.36"
    testImplementation "org.embulk:embulk-output-file:0.10.36"
    testImplementation "org.embulk:embulk-parser-csv:0.10.36"
}

embulkPlugin {
    mainClass = "org.embulk.input.gcs.GcsFileInputPlugin"
    category = "input"
    type = "gcs"
}

javadoc {
    options {
        locale = 'en_US'
        encoding = 'UTF-8'
    }
}

jar {
    from rootProject.file("LICENSE")
    from rootProject.file("NOTICE")
}

sourcesJar {
    from rootProject.file("LICENSE")
    from rootProject.file("NOTICE")
}

javadocJar {
    from rootProject.file("LICENSE")
    from rootProject.file("NOTICE")
}

// It should not publish a `.module` file in Maven Central.
// https://docs.gradle.org/current/userguide/publishing_gradle_module_metadata.html#sub:disabling-gmm-publication
tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = project.name

            from components.java

            pom {  // https://central.sonatype.org/pages/requirements.html
                packaging "jar"

                name = project.name
                description = project.description
                url = "https://www.embulk.org/"

                developers {
                    developer {
                        name = "Satoshi Akama"
                        email = "satoshiakama@gmail.com"
                    }
                    developer {
                        name = "Muga Nishizawa"
                        email = "muga.nishizawa@gmail.com"
                    }
                    developer {
                        name = "Naotoshi Seo"
                        email = "sonots@gmail.com"
                    }
                    developer {
                        name = "Robert Nguyen"
                        email = "ng.hung83@gmail.com"
                    }
                    developer {
                        name = "Hieu Duong"
                        email = "duongminhhieu89@gmail.com"
                    }
                    developer {
                        name = "John Luong"
                        email = "jluong@treasure-data.com"
                    }
                    developer {
                        name = "Dai MIKURUBE"
                        email = "dmikurube@acm.org"
                    }
                }

                licenses {
                    license {
                        // http://central.sonatype.org/pages/requirements.html#license-information
                        name = "The Apache License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }

                scm {
                    connection = "scm:git:git://github.com/embulk/embulk-input-gcs.git"
                    developerConnection = "scm:git:git@github.com:embulk/embulk-input-gcs.git"
                    url = "https://github.com/embulk/embulk-input-gcs"
                }
            }
        }
    }

    repositories {
        maven {  // publishMavenPublicationToMavenCentralRepository
            name = "mavenCentral"
            if (project.version.endsWith("-SNAPSHOT")) {
                url "https://oss.sonatype.org/content/repositories/snapshots"
            } else {
                url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            }

            credentials {
                username = project.hasProperty("ossrhUsername") ? ossrhUsername : ""
                password = project.hasProperty("ossrhPassword") ? ossrhPassword : ""
            }
        }
    }
}

signing {
    sign publishing.publications.maven
}

gem {
    authors = [ "Satoshi Akama" ]
    email = [ "satoshiakama@gmail.com" ]
    summary = "Google Cloud Storage input plugin for Embulk"
    homepage = "https://github.com/embulk/embulk-input-gcs"
    licenses = [ "Apache-2.0" ]
    from rootProject.file("LICENSE")
    from rootProject.file("NOTICE_GEM")
    rename ("NOTICE_GEM", "NOTICE")
}

gemPush {
    host = "https://rubygems.org"
}

test {
    jvmArgs '-XX:MaxPermSize=128M', '-Xmx2048m'
    testLogging {
        outputs.upToDateWhen { false }
        exceptionFormat = org.gradle.api.tasks.testing.logging.TestExceptionFormat.FULL
        showCauses = true
        showExceptions = true
        showStackTraces = true
        showStandardStreams = true
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.14.1'
}
checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = false
}
checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = false
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}
